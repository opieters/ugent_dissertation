---
title: Microcontroller Code
date:   "2018-09-08"
tags: [Dissertation]
type: "chapter"
author: "Olivier Pieters"
description: "description"
keywords: ["key", "words"]
topics: ["topic 1"]
tags: ["one", "two"]
chapter: A2
---

In this chapter, we list the most important sections of the code code. Some explanations are also provided to make the operations more clear.

# Digital LIA Implementations for First and Second Order Harmonic Signals#  

```assembly
; include common definitions
    .nolist
    .include    "dspcommon.inc"
    .list

    .section .dspExtensions, code

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;    _quadratureMult: Multiply an input signal sampled at 4X the
;            carrier frequency by both cosine and sine signals.
;            Also known as quadrature sampling with digital mixing
;            Since we are sampling at 4 x fc then the i(n) signal
;            is generated by multiplying the input x(n) by 1,0,-1,0
;            and q(n) by 0,-1,0,1. Which can be expressed without
;            fractional multiplies.
;
;    W0 = N, number of sample points
;    w1 = x(n), pointer to input sample array
;    w2 = i(n), pointer to location for i(n) data to be placed
;    w3 = q(n), pointer to location for q(n) data to be placed
;    w4 = DCOffset
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

    .global _quadratureMult

; this version is 3 + 8 * (N/4) = 83 cycles for 40 elements
    .global _quadrature_mult_1f

; this version is 3 + 8 * (N/4) = 83 cycles for 40 elements
_quadratureMult:
        push        w5

    lsr        w0, #2, w0        ; divide number of samples by 4
    dec        w0, w0            ; adjust for number of data entries

    do        w0, _endquadMult
    add        w4, [w1++], [w2++]    ; i(n) = 1 * x(n) + DCOffset
    clr	       [w3++]
    add        w4, [w1++], [w3++]    ; q(n+1) = 1 * x(n+1) + DCOffset
    clr	       [w2++]
    add        w4, [w1++], w5
    neg        w5, [w2++]            ; i(n+2) = -1 * x(n+2)
    clr	       [w3++]
    add        w4, [w1++], w5
    clr		[w2++]
_endquadMult:
    neg        w5, [w3++]        ; q(n+3) = -1 * x(n+3)
    pop        w5

    return

    .global _LIA_1f


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;    _quadrature_mult_1f: Multiply an input signal sampled at 8X 
;            the carrier frequency by both cosine and sine signals.
;            Also known as quadrature sampling with digital mixing.
;
;    W0 = N, number of sample points
;    w1 = x(n), pointer to input sample array
;    w2 = i(n), pointer to location for i(n) data to be placed
;    w3 = q(n), pointer to location for q(n) data to be placed
;    w4 = DCOffset
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

_quadrature_mult_1f:
    push           w5
    push           w6
    push           w7
    push           w10
    push           CORCON
    fractsetup     w10               ; set ALU for fractional computations

    mov        #0x5A82, w6           ; sqrt(2)/2
    neg        w6, w7                ; -sqrt(2)/2
    neg        w4, w10               ; w10 = -DC

    lsr        w0, #3, w0            ; divide number of samples by 8; w0 /=8
    dec        w0, w0                ; adjust for number of data entries; w0--

    do        w0, _end_lia_1f        ; execute code through PC+_endquadMult (incl.) w0+1 times

    add        w4, [w1++], [w2++]    ; i(n) = DC + x(n)
    clr        [w3++]                ; q(n) = 0

    clr        A                     ; clear accumulator A
    add        w4, [w1++], w5        ; w6 = DC + x[i]
    mac        w5*w6, A              ; A = w5*w6 = sqrt(2)/2*(x[n]+DC)
    sac.r        A, [w2++]           ; q[i] = A (rounded)
    sac.r        A, [w3++]           ; q[i] = A (rounded)

    clr        [w2++]                ; i(n) = 0
    add        w4, [w1++], [w3++]    ; q(n) = DC + x(n)

    clr        A                     ; clear accumulator A
    add        w4, [w1++], w5
    mac        w5*w6, A
    sac.r      A, [w3++]
    neg        A
    sac.r      A, [w2++]


    sub        w10, [w1++], [w2++]    ; i(i) = - DC - x(i)
    clr        [w3++]

    clr        A
    add        w4, [w1++], w5
    mac        w7*w5, A
    sac.r      A, [w2++]
    sac.r      A, [w3++]

    clr        [w2++]
    sub        w10, [w1++], [w3++]

    clr        A
    add        w4, [w1++], w5
    mac        w5*w6, A
    sac.r      A, [w2++]
    neg        A
_end_lia_1f:
    sac.r      A, [w3++]

    pop        CORCON
    pop        w10
    pop        w7
    pop             w6
    pop        w5

    return

```

The following license agreement applied to the `_quadratureMult` function:

```plain
/* SOFTWARE LICENSE AGREEMENT:
* Microchip Technology Incorporated ("Microchip") retains all ownership 
* and intellectual property rights in the code accompanying this message 
* and in all derivatives hereto.  You may use this code, and any 
* derivatives created by any person or entity by or on your behalf, 
* exclusively with Microchip's proprietary products.  Your acceptance 
* and/or use of this code constitutes agreement to the terms and 
* conditions of this notice.
*
* CODE ACCOMPANYING THIS MESSAGE IS SUPPLIED BY MICROCHIP "AS IS".  NO
* WARRANTIES, WHETHER EXPRESS, IMPLIED OR STATUTORY, INCLUDING, BUT NOT 
* LIMITED TO, IMPLIED WARRANTIES OF NON-INFRINGEMENT, MERCHANTABILITY 
* AND FITNESS FOR A PARTICULAR PURPOSE APPLY TO THIS CODE, ITS 
* INTERACTION WITH MICROCHIP'S PRODUCTS, COMBINATION WITH ANY OTHER 
* PRODUCTS, OR USE IN ANY APPLICATION.
*
* YOU ACKNOWLEDGE AND AGREE THAT, IN NO EVENT, SHALL MICROCHIP BE LIABLE, 
* WHETHER IN CONTRACT, WARRANTY, TORT (INCLUDING NEGLIGENCE OR BREACH OF 
* STATUTORY DUTY), STRICT LIABILITY, INDEMNITY, CONTRIBUTION, OR 
* OTHERWISE, FOR ANY INDIRECT, SPECIAL, PUNITIVE, EXEMPLARY, INCIDENTAL 
* OR CONSEQUENTIAL LOSS, DAMAGE, FOR COST OR EXPENSE OF ANY KIND 
* WHATSOEVER RELATED TO THE CODE, HOWSOEVER CAUSED, EVEN IF MICROCHIP 
* HAS BEEN ADVISED OF THE POSSIBILITY OR THE DAMAGES ARE FORESEEABLE.  
* TO THE FULLEST EXTENT ALLOWABLE BY LAW, MICROCHIP'S TOTAL LIABILITY 
* ON ALL CLAIMS IN ANY WAY RELATED TO THIS CODE, SHALL NOT EXCEED THE 
* PRICE YOU PAID DIRECTLY TO MICROCHIP SPECIFICALLY TO HAVE THIS CODE 
* DEVELOPED.
*
* You agree that you are solely responsible for testing the code and
* determining its suitability.  Microchip has no obligation to modify, test,
* certify, or support the code.
********************************************************************/

This applies to the following files:
* lock_in_amplifier.s _quadratureMult routine
* traps.c
* dspcommon.inc
* foldedFIR.s
```
